C     THIS CODE CALLS THE PROGRAM MAND4F WHICH DOES THE ACTUAL FRACTAL
C     COMPUTATION.  FOR EXPLANATION OF THE VARIOUS FORTRAN STATEMENTS
C     USED YOU SHOULD READ THE DOCUMENTATION IN MAND4F AS IT IS NOT
C     REPEATED HERE.
C
C     COPYRIGHT (C) 1989 BY ART MATRIX.  ALL RIGHTS RESERVED.
C        PO 880 ITHACA, NY 14851-0880 USA (607) 277-0959
C           CORNELL NATIONAL SUPERCOMPUTER FACILITY
C       DUPLICATION PERMITTED FOR NON COMMERCIAL PURPOSES.
C
           REAL*8  XL,YL,SIDE
C
        INTEGER*2  OUT(0:400,0:400)
        INTEGER*4  COL(0:400,0:400)
C
C     COL IS A 401 BY 401 MATRIX OF 4 BYTE INTEGERS WHICH WILL
C     HOLD THE FRACTAL DATA PASSED BACK FROM MAND4F.  BEFORE
C     IT IS WRITTEN OUT TO DISK HOWEVER, THE DATA IS TRANSFERED
C     TO THE VARIABLE 'OUT' WHICH IS MADE OF 2 BYTE INTEGERS.  THIS
C     SAVES EXTENSIVE ROOM ON DISK WHERE THE IMAGES ARE STORED
C     AND HAS NO OTHER SIGNIFICANCE.
C
      CHARACTER*8  TYPE,TITLE,TIME
C
C     EACH FRACTAL IMAGE HAS A FIRST NAME (TITLE) AND A SECOND NAME
C     (TYPE).  FOR EXAMPLE  M23 MANDIN.  THE TIME VARIABLE HOLDS
C     THE ELAPSED TIME OF COMPUTATION, OFTEN THE REAL TIME THAT
C     ELAPSED BUT RECENTLY CHANGED TO ACTUAL COMPUTER TIME USED.
C
C***********************************************************************
C***********************************************************************
C
      READ   (5,1,END=9999) TITLE
      READ   (5,1,END=9999) TYPE
1     FORMAT (A8)
C
C     THE VALUES OR TITLE AND TYPE ARE STACKED BY THE CALLING EXEC
C     (MANDIN EXEC) AND ARE READ IN AT THIS POINT TO THEIR RESPECTIVE
C     VARIABLES.
C     THE FORTRAN READ STATEMENT HAS 3 PARTS, THE UNIT NUMBER (5) WHICH
C     REFERS TO WHERE IT SHOULD READ FROM (5 MEANS THE CONSOLE OR THE
C     STACK IF THERE IS SOMETHING IN IT), THE FORMAT STATEMENT NUMBER
C     (1), WHICH REFERS TO THE STATEMENT NUMBER HOLDING THE FORMAT
C     WHICH TELLS THE READ STATEMENT WHAT FORMAT THE DATA
C     IS IN, AND THE END BRANCH (END=9999) WHICH TELLS THE STATEMENT
C     NUMBER TO BRANCH TO IF THE READ SHOULD FAIL FOR ANY REASON OR
C     IF AN EMPTY RETURN SHOULD BE HIT WHEN THE READ IS LOOKING
C     FOR DATA.
C     THE FORMAT (A8) STATEMENT SAYS THAT THE DATA WILL BE 8 BYTES
C     OF ALPHANUMERIC OR CHARACTER DATA.
C
      WRITE (6,*) ' ENTER LOWER LEFT CORNER AS X,Y'
      READ  (5,*,END=9999) XL,YL
      WRITE (6,*) XL,YL
C
C     THE FORTRAN WRITE STATEMENT HAS TWO PARTS, THE UNIT NUMBER (6)
C     WHICH LIKE THE READ STATEMENT TELLS IT WHERE TO WRITE TO, IN
C     THIS CASE THE CONSOLE (5 AND 6 ARE BOTH CONSOLE), AND
C     THE FORMAT STATEMENT NUMBER WHICH IN THIS CASE IS A * MEANING
C     THERE IS NO FORMAT STATEMENT AND THE WRITE SHOULD PROCEED
C     ACCORDING TO DEFAULT CONCEPTS.
C     LIKEWISE IN THE READ STATEMENT JUST BELOW, THE * MEANS THE
C     DATA CAN BE READ IN IN A FREE FORMAT KIND OF STRING.  THE
C     COMPILER WILL FIGURE OUT WHAT THE DATA IS BY CONTEXT.
C     SO THE ABOVE THREE STATEMENTS, PROMPT THE USER FOR LOWER LEFT
C     X AND Y, THEN A READ IS ISSUED TO READ THEM IN FROM THE
C     KEYBOARD, AND THEN THEY ARE WRITTEN BACK OUT AS A RECORD
C     OF WHAT WAS TYPED IN.
C
      WRITE (6,*) ' ENTER SIDEX, SIDEY AND NUMBER OF ITERATIONS'
      READ  (5,*,END=9999) SIDE,SIDE,NITER
      WRITE (6,*) SIDE,SIDE,NITER
C
C     MOST FRACTAL IMAGES CAN HAVE NON SQUARE SIDES, THUS THE NEED
C     FOR AN X SIDE AND A Y SIDE.  IN THIS CASE HOWEVER WE WANT
C     BOTH NUMBERS TO BE THE SAME SO THE VARIABLE 'SIDE' IS USED
C     TWICE FOR BOTH X AND Y.  IF PERCHANCE TWO DIFFERENT VALUES
C     WERE TO BE TYPED IN FOR SIDE, THE SECOND ONE WOULD BE
C     THE ONE THAT WAS USED.
C
      CALL TCLR2
C
C     AN INSIGNIFICANT PROGRAM TO CLEAR THE ATOMIC CLOCK.
C
      CALL MAND4F (XL,YL,SIDE,NITER,COL)
C
C     MAIN COMPUTATION ROUTINE TAKING IN X LOW, Y LOW, SIDE LENGTH,
C     NUMBER OF ITERATIONS AND THE COLOR MATRIX.   THESE ARE PASSED
C     AS ADDRESSES TO THE CALLED SUBROUTINE.  THUS WHEN THE CALLED
C     SUBROUTINE CHANGES THEIR VALUES (COL IN SPECIFIC) THEIR VALUES
C     RETAIN THE NEW VALUES AFTER THE SUBROUTINE ENDS.
C     RARELY IS THERE ANY NEED TO CHANGE XL, YL, SIDE OR NITER INSIDE
C     THE COMPUTING SUBROUTINE.
C
      CALL TREAD2 (TIME)
C
C     READS THE ATOMIC CLOCK AND STORES THE ELAPSED TIME IN THE
C     VARIABLE 'TIME' TO GIVE SOME INDICATION HOW LONG THE COMPUTATION
C     TOOK.
C     AGAIN THIS IS INSIGNIFICANT TO THOSE OF YOU WITH PC'S AND CAN
C     BE IGNORED.
C
C***********************************************************************
C***********************************************************************
C
      DO 20 J = 0,400
      DO 20 I = 0,400
20    OUT(I,J) = COL(I,J)
C
C     THESE TWO DO LOOPS TRANSFER ALL THE COLOR DATA FROM COL TO OUT,
C     CHANGING THEIR FORMAT FROM 4 BYTE INTEGERS TO 2 BYTE INTEGERS.
C
C***********************************************************************
C***********************************************************************
C
C     THE FOLLOWING CODE TO THE END OF THIS SUBROUTINE IS MACHINE
C     SPECIFIC INSTRUCTIONS TO WRITE THE DATA IN 'OUT' TO THE DISK,
C     AND TO CREATE AN APPROPRIATE PARAMETER FILE CONTAINING THE
C     LOW AND SIDE LENGTHS, NUMBER OF ITERATIONS AND PROGRAM NAME.
C     THOSE OF YOU WITH PC'S SHOULD IGNORE THIS ALL TOGETHER AS YOU
C     WILL HAVE TO DESIGN YOUR OWN CODE TO WRITE OUT THE FRACTAL
C     IMAGE TO DISK IN WHAT EVER MANNER IS APPROPRIATE TO YOUR PC.
C
C     OUT IMAGES HAVE TWO FILES ASSOCIATED WITH THEM, ONE THE DATA
C     FILE AND THE OTHER THE PARAMETER FILE.  THE IMAGE M23 FOR
C     EXAMPLE WOULD HAVE  M23 D_MANDIN  AS ITS DATA FILE AND
C     M23 P_MANDIN  AS ITS PARAMETER FILE.
C
C          DATA FILE DEFINITION.
C
      CALL CMS ('FI 3 CLEAR')
      CALL CMS
     * ('FI 3 DISK '//TITLE//' D_'//TYPE//' A (RECFM VBS')
C
      CALL OUTRLE (OUT,160800)
C
C          PARAMETER FILE DEFINITION.
C
      CALL CMS ('FI 2 CLEAR')
      CALL CMS
     * ('FI 2 DISK '//TITLE//' P_'//TYPE//' A (RECFM F BLOCK 80')
C
      CALL PRMHD2 (3,TITLE,TYPE,TIME)
C
      WRITE (2,100) 10,420,21,1
      WRITE (2,200) XL,YL,SIDE,SIDE,NITER,5
C
100   FORMAT ('   PARM:   DATA:    MAP:   PROC:'/4I8/)
200   FORMAT (
     * E30.20,  ' X LOW'/
     * E30.20,  ' Y LOW'/
     * E30.20,  ' X SIDE'/
     * E30.20,  ' Y SIDE'/
     * I30,     ' ITERATIONS'/
     * I30,     ' MODULO')
C
      CLOSE (2)
      CLOSE (3)
C
C***********************************************************************
C***********************************************************************
C
      CALL SAVER (TITLE,TYPE,IRC)
      IF (IRC .NE. 2) CALL CMS ('CP LOGOUT')
C
C***********************************************************************
C***********************************************************************
C
9999  RETURN
      END
